---
layout: post
title: Solidity：在合约中创建其它合约
tags: [blockchain, solidity]
mermaid: false
math: false
--- 

在Solidity中，`new`关键字用于创建一个新的智能合约实例。当你使用`new`关键字创建一个新的合约实例时，Solidity会在区块链上部署一个新的合约，并返回新合约的地址。自0.8.0版本开始，`new`关键字通过指定`salt`选项支持`create2`特性。

以下是使用`new`关键字创建新的合约实例的基本语法：

```solidity
ContractName variableName = new ContractName(arguments);
```

在这里，`ContractName`是你要创建的合约的名称，`variableName`是你要给新创建的合约实例的变量名，`arguments`是传递给新合约构造函数的参数（如果有的话）。

例如，假设你有一个名为`MyContract`的合约，它有一个接受一个`uint`类型参数的构造函数，你可以使用以下代码创建一个新的`MyContract`实例：

```solidity
MyContract myContract = new MyContract(123);
```

在这个例子中，`new MyContract(123)`会在区块链上部署一个新的`MyContract`合约，并将构造函数的参数设置为`123`。然后，它会返回新合约的地址，并将这个地址赋值给`myContract`变量。

需要注意的是，使用`new`关键字创建新的合约实例会消耗gas，因为它涉及到在区块链上部署新的合约。因此，你需要确保你有足够的gas来完成这个操作。此外，新创建的合约的代码和数据将被永久存储在区块链上，因此，你需要谨慎地管理你的合约代码和数据，以避免浪费存储空间。  

## 示例  

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.25;

contract Car {
    address public owner;
    string public color;

    constructor(address _owner, string memory _color) {
        owner = _owner;
        color = _color;
    }

    function getOwner() public view returns (address) {
        return owner;
    }

    function getColor() public view returns (string memory) {
        return color;
    }
}

contract CarStore {
    Car[] public cars;

    function create(address _owner, string memory _color) public {
        Car car = new Car(_owner, _color);
        cars.push(car);
    }

    function createWithSalt(
        address _owner,
        string memory _color,
        bytes32 _salt
    ) public {
        Car car = (new Car){salt: _salt}(_owner, _color);
        cars.push(car);
    }

    function getCar(uint256 index)
        public
        view
        returns (address, string memory)
    {
        Car car = cars[index];
        // 即使变量被声明为public，我们也不能在合约外部直接访问它们。只能通过调用自动生成的getter函数来访问这些变量。
        // return (car.owner,car.color);    // 会报错
        // return (car.owner(),car.color());
        return (car.getOwner(), car.getColor());
    }
}
```  

上面的示例中包含两个合约：`Car`和`CarStore`：  

- `Car`合约代表一辆汽车，它有两个状态变量：`owner`和`color`，分别表示汽车的所有者和颜色。这两个状态变量都被声明为`public`，因此Solidity会自动为它们生成getter函数。此外，`Car`合约还有两个自定义的getter函数：`getOwner`和`getColor`，它们分别返回汽车的所有者和颜色。
- `CarStore`合约代表一个汽车商店，它有一个状态变量`cars`，用于存储商店中的所有汽车。`cars`变量是一个`Car`合约的数组，每个元素都是一个`Car`合约的实例。
  - `create`函数：创建一个新的`Car`合约实例，并将其添加到`cars`数组中。这个函数接受两个参数：汽车的所有者和颜色。
  - `createWithSalt`函数：与`create`函数类似，但它使用`create2`特性创建新的`Car`合约实例。`create2`特性允许你使用一个`salt`值来影响新合约的地址。这个函数接受三个参数：汽车的所有者、颜色和`salt`值。
  - `getCar`函数：返回`cars`数组中指定索引的汽车的所有者和颜色。这个函数接受一个参数：汽车的索引。

---

<div align="center">
  <img src="../img/qrcode_wechat.jpg" alt="孟斯特">
</div>

> 声明：本作品采用[署名-非商业性使用-相同方式共享 4.0 国际 (CC BY-NC-SA 4.0)](https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh)进行许可，使用时请注明出处。  

> Author: [mengbin](mengbin1992@outlook.com)  

> blog: [mengbin](https://mengbin.top)  

> Github: [mengbin92](https://mengbin92.github.io/)  

> cnblogs: [恋水无意](https://www.cnblogs.com/lianshuiwuyi/)  

> 腾讯云开发者社区：[孟斯特](https://cloud.tencent.com/developer/user/6649301)  

---
